plugins {
	id 'groovy'
	id 'java-gradle-plugin'

//	id "com.github.sebersole.testkit-junit5" version "1.3.0"

	// for publishing to the plugin portal
	id 'com.gradle.plugin-publish' version '0.11.0'
	id "nu.studer.credentials" version "2.1"

	id 'idea'
	id 'eclipse'
}

repositories {
	mavenCentral()
}

ext {
	pluginId = 'org.hibernate.build.xjc'
	pluginVersion = '2.1.5-SNAPSHOT'

	//noinspection GrUnresolvedAccess
	project.setProperty( 'gradle.publish.key', credentials.hibernatePluginPortalUsername )
	//noinspection GrUnresolvedAccess
	project.setProperty( 'gradle.publish.secret', credentials.hibernatePluginPortalPassword )
}


group = 'org.hibernate.build.gradle'
version = ext.pluginVersion

sourceCompatibility = 1.8
targetCompatibility = 1.8

buildDir = 'target'

dependencies {
	implementation gradleApi()
	implementation localGroovy()

	testImplementation gradleApi()
	testImplementation "org.junit.jupiter:junit-jupiter-api:5.7.1"
	testImplementation "org.junit.jupiter:junit-jupiter-engine:5.7.1"
}


gradlePlugin {
	plugins {
		xjcPlugin {
			id = project.pluginId
			implementationClass = 'org.hibernate.build.gradle.xjc.XjcPlugin'
		}
	}
}

pluginBundle {
	website = 'https://github.com/hibernate/gradle-xjc-plugin'
	vcsUrl = 'https://github.com/hibernate/gradle-xjc-plugin'
	tags = ['jaxb','xjc']

	plugins {
		xjcPlugin {
			id = project.pluginId
			displayName = 'JAXB binding model generation using the XJC tool'
			description = 'JAXB binding model generation using the XJC tool'
		}
	}
}

if ( version.toString().endsWith( '-SNAPSHOT' ) ) {
	tasks.publishPlugins.enabled = false
}

tasks.publishPlugins {
	doFirst {
		if ( credentials.hibernatePluginPortalUsername == null ) {
			throw new RuntimeException( "`hibernatePluginPortalUsername` not found" )
		}
		if ( credentials.hibernatePluginPortalPassword == null ) {
			throw new RuntimeException( "`hibernatePluginPortalPassword` not found" )
		}
	}
}

task compile
tasks.compile.dependsOn tasks.compileJava, tasks.compileTestJava

wrapper {
	// To upgrade the version of gradle used in the wrapper, run:
	//     ./gradlew wrapper --gradle-version NEW_VERSION
	distributionType = Wrapper.DistributionType.ALL
}

tasks.test {
	useJUnitPlatform()
}